import java.nio.file.Paths

plugins {
    id 'org.gosu-lang.gosu' version '0.3.10'
    id "com.bmuschko.nexus" version "2.3.1"
    id "io.codearte.nexus-staging" version "0.21.1"
}

group 'xyz.ronella.gosu'
version '1.0.0'

sourceCompatibility = 1.8

repositories {
    maven {
        url 'https://repo.ronella.xyz/artifactory/java-central/'
        credentials {
            username "${artifactoryUsername}"
            password "${artifactoryPassword}"
        }
    }
}

modifyPom {
    project {
        name 'GRegistry'
        description 'A simple gosu implementation of code registry where you can retrieve a group of classes marked by custom annotation.'
        url 'https://github.com/rcw3bb/gregistry'
        inceptionYear '2020'

        scm {
            url 'https://github.com/rcw3bb/gregistry'
            connection 'https://github.com/rcw3bb/gregistry.git'
            developerConnection 'https://github.com/rcw3bb/gregistry.git'
        }

        licenses {
            license {
                name 'MIT License'
                url 'https://github.com/rcw3bb/gregistry/blob/master/LICENSE.md'
                distribution 'repo'
            }
        }

        developers {
            developer {
                id 'rcw3bb'
                name 'Ronaldo Webb'
                email 'ronaldo.c.webb@gmail.com'
            }
        }
    }
}

extraArchive {
    sources = true
    tests = true
    javadoc = true
}

nexus {
    sign = true
    repositoryUrl = 'https://oss.sonatype.org/service/local/staging/deploy/maven2'
    snapshotRepositoryUrl = 'https://oss.sonatype.org/content/repositories/snapshots'
}

nexusStaging {
    stagingProfileId = "33650220e7857c"
}

dependencies {
    compile group: 'org.gosu-lang.gosu', name: 'gosu-core', version: '1.13.29'
    compile group: 'org.slf4j', name: 'slf4j-api', version: '1.7.25'
    compile group: 'xyz.ronella.gosu', name: 'gcache', version: '1.0.1'
    compile fileTree(dir: './libs', include: ['*.jar'])

    testCompile group: 'org.gosu-lang.gosu', name: 'gosu-test', version: '1.13.29'
    testCompile group: 'org.apache.logging.log4j', name: 'log4j-slf4j-impl', version: '2.9.1'
    testRuntime files(Paths.get(JAVA_TOOLS_JAR))
}

task preparePackage(dependsOn: [jar]) {
    doLast {
        def srcDir = sourceSets.main.gosu.sourceDirectories.asPath
        def destDir = Paths.get(buildDir.absolutePath, "gosu")

        copy {
            from srcDir
            into destDir
        }
    }
}

task jarGosu(dependsOn: [preparePackage]) {
    doLast {
        def srcDir = Paths.get(buildDir.absolutePath, "gosu")
        def jarFilename ="${project.name}-${version}"
        def jarGosuFile = "${jarFilename}-gosu.jar"
        def javaLibsDir = Paths.get(buildDir.absolutePath, "java-libs")
        def gosuLibsDir = Paths.get(buildDir.absolutePath, "gosu-libs")
        def outputGosuFile = Paths.get(gosuLibsDir.toFile().absolutePath, jarGosuFile)
        def libsDir = Paths.get(buildDir.absolutePath, "libs")
        def outputFile = Paths.get(libsDir.toFile().absolutePath, "${jarFilename}.jar")

        ant.zip(destfile: outputGosuFile) {
            fileset(dir: srcDir)
        }

        ant.move file: outputFile, toDir: javaLibsDir

        copy {
            from outputGosuFile
            into libsDir
            rename { filename ->
                filename.replace jarGosuFile, "${jarFilename}.jar"
            }
        }
    }
}

jar.finalizedBy(jarGosu)